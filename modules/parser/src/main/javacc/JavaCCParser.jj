options {
  JDK_VERSION="1.8";
  JAVA_TEMPLATE_TYPE = "modern";
  SUPPORT_CLASS_VISIBILITY_PUBLIC = false;
  UNICODE_INPUT = true;
  JAVA_UNICODE_ESCAPE = false;
}

PARSER_BEGIN(JavaCCParser)

package org.dhallj.parser.support;

import java.util.AbstractMap.SimpleImmutableEntry;
import java.util.ArrayDeque;
import java.util.ArrayList;
import java.util.Deque;
import java.util.List;
import java.util.Map;
import org.dhallj.core.Operator;
import org.dhallj.core.Expr;

final class JavaCCParser {
  private static final String trimLabel(String input) {
    return (input.charAt(0) != '`') ? input : input.substring(1, input.length() - 1);
  }
}

PARSER_END(JavaCCParser)

TOKEN_MGR_DECLS : {
  Deque<Boolean> interpolationState = new ArrayDeque<>();
  Deque<Integer> braceDepth = new ArrayDeque<>();
  List<Comment> comments = new ArrayList<>();
  WhitespaceManager whitespace = new WhitespaceManager();
}

TOKEN: {
    <WHSP: ([" ", "\t", "\n"] | "\r\n")+ | "{-" | "--"> {
      List<Comment> cs = whitespace.consume(input_stream, matchedToken.image.charAt(0));
      if (cs != null) {
        comments.addAll(cs);
      }
    }
  | <DOUBLE_LITERAL: ((<SIGN>)? <DIGITS> (("." <DIGITS> (<EXPONENT>)?) | <EXPONENT>)) | (("-")? "Infinity") | "NaN">
  | <INTEGER_LITERAL: <SIGN> <NATURAL_LITERAL>>
  | <NATURAL_LITERAL: "0" | (["1"-"9"] (<DIGIT>)*) | ("0x" <HEX_DIGITS>)>

  | <IF: "if">
  | <THEN: "then">
  | <ELSE: "else">
  | <LET: "let">
  | <IN: "in">
  | <AS: "as">
  | <USING: "using">
  | <MERGE: "merge">
  | <MISSING: "missing">
  | <SOME: "Some">
  | <TOMAP: "toMap">
  | <ASSERT: "assert">
  | <FORALL: "forall" | "âˆ€">
  | <WITH: "with">
  | <TEXT: "Text">
  | <LOCATION: "Location">
  | <BUILT_IN:
      "Natural/fold"
    | "Natural/build"
    | "Natural/isZero"
    | "Natural/even"
    | "Natural/odd"
    | "Natural/toInteger"
    | "Natural/show"
    | "Integer/toDouble"
    | "Integer/show"
    | "Integer/negate"
    | "Integer/clamp"
    | "Natural/subtract"
    | "Double/show"
    | "List/build"
    | "List/fold"
    | "List/length"
    | "List/head"
    | "List/last"
    | "List/indexed"
    | "List/reverse"
    | "Text/show"
    | "Bool"
    | "True"
    | "False"
    | "None"
    | "Natural"
    | "Integer"
    | "Double"
    | "Text"
    | "List"
    | "Type"
    | "Kind"
    | "Sort"
    | "Optional"
    | "List"
  >
  | <QUOTED_LABEL: "`" (["\u0020"-"\u005f"] | ["\u0061"-"\u007e"])* "`">
  | <SIMPLE_LABEL: (<ALPHA> | "_") (<ALPHA> | <DIGIT> | "_" | "-" | "/" )*>
  | <LAMBDA: "\\" | "\u03bb">
  | <ARROW: "->" | "\u2192">
  | <COMPLETE: "::">

  | <OR: "||">
  | <AND: "&&">
  | <EQUALS: "==">
  | <NOT_EQUALS: "!=">
  | <PLUS: "+">
  | <TIMES: "*">
  | <TEXT_APPEND: "++">
  | <LIST_APPEND: "#">
  | <COMBINE: "/\\" | "\u2227">
  | <PREFER: "//" | "\u2afd">
  | <COMBINE_TYPES: "//\\\\" | "\u2a53">
  | <IMPORT_ALT: "?">
  | <EQUIVALENT: "===" | "\u2261">

  | <DOUBLE_QUOTE_START: "\"">: WITHIN_DOUBLE_QUOTE
  | <SINGLE_QUOTE_START: "''" ("\n" | "\r\n")>: WITHIN_SINGLE_QUOTE
  | <PARENS_OPEN: "(">
  | <PARENS_CLOSE: ")">
  | <BRACKET_OPEN: "[">
  | <BRACKET_CLOSE: "]">
  | <BRACE_OPEN: "{"> { if (braceDepth.isEmpty()) { braceDepth.push(0); }; braceDepth.push(braceDepth.pop() + 1); ; }
  | <BRACE_CLOSE: "}"> {
    int currentBraceDepth = braceDepth.pop() - 1;
    boolean currentInterpolationState = false;
    if (currentBraceDepth >= 0) {
      braceDepth.push(currentBraceDepth);
    } else {
      currentInterpolationState = interpolationState.pop();
    }
    SwitchTo((currentBraceDepth >= 0)
      ? DEFAULT
      : (currentInterpolationState) ? WITHIN_DOUBLE_QUOTE : WITHIN_SINGLE_QUOTE);
  }
  | <ANGLE_OPEN: "<">
  | <ANGLE_CLOSE: ">">
  | <DOT: ".">
  | <COLON: ":">
  | <COMMA: ",">
  | <BAR: "|">
  | <EQUAL_SIGN: "=">
  | <AT_SIGN: "@">
  | <SHA256_HASH: "sha256:" (<HEX_DIGIT>){64}>
  | <ENV: "env:" (<BASH_ENV_VAR> | ("\"" <POSIX_ENV_VAR> "\""))>
  | <HTTP: "http" ("s")? "://" (<USER_INFO> "@")? <HOST> (":" (<DIGIT>)*)? <URL_PATH> ("?" <QUERY>)?>
  | <LOCAL: <PARENT_PATH> | <HERE_PATH> | <HOME_PATH> | <PATH>>
  | <CLASSPATH: "classpath:" <PATH> >
  | <#DIGIT: ["0"-"9"]>
  | <#DIGITS: (<DIGIT>)+>
  | <#HEX_DIGIT: <DIGIT> | ["A"-"F"] | ["a"-"f"]>
  | <#HEX_DIGITS: (<HEX_DIGIT>)+>
  | <#SIGN: "+" | "-">
  | <#EXPONENT: ("E" | "e") (<SIGN>)? <DIGITS>>
  | <#ALPHA: ["A"-"Z"] | ["a"-"z"]>
  | <#ASCII: ["\u0020"-"\u007f"]>
  | <#VALID_NON_ASCII: ["\u0080"-"\uD7FF"] | (["\ud800"-"\udbff"] ["\udc00"-"\udfff"]) | ["\ue000"-"\ufffd"]>
  | <#BASH_ENV_VAR: (<ALPHA> | "_") (<ALPHA> | <DIGIT> | "_")*>
  | <#POSIX_ENV_VAR: ("\u0020" | "\u0021" | ["\u0023"-"\u003c"] | ["\u003e"-"\u005b"] | ["\u005d"-"\u007e"] | ("\\" (["\"", "\\", "a", "b", "f", "n", "r", "t", "v"])))+>

  | <#PCT_ENCODED: "%" <HEX_DIGIT> <HEX_DIGIT>>
  | <#SUB_DELIM: ["!", "$", "&", "'", "*", "+", ";", "="]>
  | <#UNRESERVED: <ALPHA> | <DIGIT> | ["-", ".", "_", "~"]>
  | <#PCHAR: <UNRESERVED> | <PCT_ENCODED> | <SUB_DELIM> | ":" | "@">
  | <#SEGMENT: (<PCHAR>)*>
  | <#USER_INFO: (<UNRESERVED> | <PCT_ENCODED> | <SUB_DELIM> | ":")*>
  | <#QUERY: (<PCHAR> | "/" | "?")*>
  | <#DOMAIN_LABEL: (<ALPHA> | <DIGIT>)+ (("-")+ (<ALPHA> | <DIGIT>)+)*>
  | <#DOMAIN: <DOMAIN_LABEL> ("." <DOMAIN_LABEL>)* (".")?>
  | <#IPV4: <DEC_OCTET> "." <DEC_OCTET> "." <DEC_OCTET> "." <DEC_OCTET>>
  | <#H16: (<HEX_DIGIT>){1,4}>
  | <#LS32: (<H16> ":" <H16>) | <IPV4>>
  | <#IPV6:
      ((<H16> ":"){6} <LS32>)
    | ("::" (<H16> ":"){5} <LS32>)
    | ((<H16>)? "::" (<H16> ":"){4} <LS32>)
    | ((<H16> (":" <H16>){0, 1})? "::" (<H16> ":"){3} <LS32>)
    | ((<H16> (":" <H16>){0, 2})? "::" (<H16> ":"){2} <LS32>)
    | ((<H16> (":" <H16>){0, 3})? "::" <H16> ":" <LS32>)
    | ((<H16> (":" <H16>){0, 4})? "::" <LS32>)
    | ((<H16> (":" <H16>){0, 5})? "::" <H16>)
    | ((<H16> (":" <H16>){0, 6})? "::")
    >
  | <#IPVFUTURE: "v" (<HEX_DIGIT>)+ "." (<UNRESERVED> | <SUB_DELIM> | ":")+>
  | <#DEC_OCTET: ("25" ["0"-"5"]) | ("2" ["0"-"4"] <DIGIT>) | ("1" <DIGIT> <DIGIT>) | (["1"-"9"] <DIGIT>) | <DIGIT>>
  | <#HOST: <DOMAIN> | <IPV4> | ("[" (<IPV6> | <IPVFUTURE>) "]")>
  | <#PATH_CHARACTER:
      ["\u0021", "\u003d", "\u007c", "\u007e"]
    | ["\u0024"-"\u0027"] | ["\u002a"-"\u002b"] | ["\u002d"-"\u002e"]
    | ["\u0030"-"\u003b"] | ["\u0040"-"\u005a"] | ["\u0040"-"\u005a"] | ["\u005e"-"\u007a"]>
  | <#QUOTED_PATH_CHARACTER: ["\u0020"-"\u0021"] | ["\u0023"-"\u002e"] | ["\u0030"-"\u007f"] | <VALID_NON_ASCII>>
  | <#PATH_COMPONENT: "/" ((<PATH_CHARACTER>)+ | ("\"" (<QUOTED_PATH_CHARACTER>)+ "\""))>
  | <#URL_PATH_COMPONENT: "/" ((<PATH_CHARACTER>)+)>
  | <#PATH: (<PATH_COMPONENT>)+>
  | <#PARENT_PATH: ".." <PATH>>
  | <#HERE_PATH: "." <PATH>>
  | <#HOME_PATH: "~" <PATH>>
  | <#URL_PATH: (<URL_PATH_COMPONENT> | ("/") <SEGMENT>)*>
}

<WITHIN_DOUBLE_QUOTE> TOKEN: {
  <DOUBLE_QUOTE_INTERPOLATION: "${"> {
    interpolationState.push(true);
    braceDepth.push(0);
    SwitchTo(DEFAULT);
  } |
  <DOUBLE_QUOTE_DOLLAR_SIGN: "$"> |
  <DOUBLE_QUOTE_CHARS: (
      "\u0020" | "\u0021" | "#" | ["\u0025"-"\u005b"] | ["\u005d"-"\u007f"]
    | ("\\" (["\"", "$", "\\", "/", "b", "f", "n", "r", "t"]))
    | ("\\u" <UNICODE_ESCAPE>)
    | <VALID_NON_ASCII>
  )+> |
  <#UNICODE_ESCAPE: <UNBRACED_ESCAPE> | ("{" <BRACED_ESCAPE> "}")> |
  <#UNBRACED_ESCAPE:
      ((<DIGIT> | "A" | "B" | "C" | "a" | "b" | "c") (<HEX_DIGIT>){3})
    | (("D" | "d") ["0"-"7"] <HEX_DIGIT> <HEX_DIGIT>)
    | (("E" | "e") (<HEX_DIGIT>){3})
    | (("F" | "f") <HEX_DIGIT> <HEX_DIGIT> ("A" | "B" | "C" | "D" | "a" | "b" | "c" | "d"))
  > |
  <#BRACED_ESCAPE: ("0")* <BRACED_CODEPOINT>> |
  <#BRACED_CODEPOINT: ((["0"-"9"] | ["A"-"F"] | ["a"-"f"] | "10") <UNICODE_SUFFIX>) | <UNBRACED_ESCAPE> | ((<HEX_DIGIT>){3})+> |
  <#UNICODE_SUFFIX: ((<DIGIT> | ["A"-"E"] | ["a"-"e"]) (<HEX_DIGIT>){3}) | (("F" | "f") <HEX_DIGIT> <HEX_DIGIT> (<DIGIT> | ["A"-"D"] | ["a"-"d"]))>
}
<WITHIN_DOUBLE_QUOTE> TOKEN: { <DOUBLE_QUOTE_END: "\"">: DEFAULT }

<WITHIN_SINGLE_QUOTE> TOKEN: {
  <SINGLE_QUOTE_INTERPOLATION: "${"> {
    interpolationState.push(false);
    braceDepth.push(0);
    SwitchTo(DEFAULT);
  } |
  <ESCAPED_QUOTE_PAIR: "'''"> |
  <ESCAPED_INTERPOLATION: "''${"> |
  <SINGLE_QUOTE_CHARS: (<ASCII> | <VALID_NON_ASCII> | "\t" | "\n" | "\r\n")>
}
<WITHIN_SINGLE_QUOTE> TOKEN: { <SINGLE_QUOTE_END: "''">: DEFAULT }

String ANY_LABEL_OR_SOME(): { Token token; } {
  (token=<SIMPLE_LABEL> | token=<QUOTED_LABEL> | token=<TEXT> | token=<LOCATION> | token=<BUILT_IN> | token=<SOME>) {
    return trimLabel(token.image);
  }
}

String NAME_BINDING(): { Token token; } {
  (token=<SIMPLE_LABEL> | token=<QUOTED_LABEL> | token=<TEXT> | token=<LOCATION>) {
    return trimLabel(token.image);
  }
}

Map.Entry<String, Expr.Parsed> DOUBLE_QUOTE_CHUNK(): {
  StringBuilder builder = null;
  Token token0 = null;
  Token token1 = null;
  Expr.Parsed expr = null;
} {
  (
      (<DOUBLE_QUOTE_INTERPOLATION> expr=COMPLETE_EXPRESSION() <BRACE_CLOSE>)
    | (
      (token0=<DOUBLE_QUOTE_DOLLAR_SIGN> | token0=<DOUBLE_QUOTE_CHARS>)
      (LOOKAHEAD(2)
        (token1=<DOUBLE_QUOTE_DOLLAR_SIGN> | token1=<DOUBLE_QUOTE_CHARS>) {
          if (builder == null) {
            builder = new StringBuilder(token0.image);
          }
          builder.append(token1.image);
        }
      )*
    )
  ) {
    if (builder == null) {
      return new SimpleImmutableEntry<>(token0 == null ? null : token0.image, expr);
    } else {
      return new SimpleImmutableEntry<>(builder.toString(), expr);
    }
  }
}

Expr.Parsed DOUBLE_QUOTE_LITERAL(): {
  List<Map.Entry<String, Expr.Parsed>> chunks = new ArrayList<>(1);
  Map.Entry<String, Expr.Parsed> current;
  Token first;
  Token last;
} {
  first=<DOUBLE_QUOTE_START>
  (current=DOUBLE_QUOTE_CHUNK() { chunks.add(current); })*
  last=<DOUBLE_QUOTE_END> {
    return ParsingHelpers.makeTextLiteral(chunks, first, last);
  }
}

List<Map.Entry<String, Expr.Parsed>> SINGLE_QUOTE_CONTINUE(): {
  List<Map.Entry<String, Expr.Parsed>> continuation = null;
  Token token = null;
  Expr.Parsed expr = null;
} {

    (
      <SINGLE_QUOTE_INTERPOLATION> expr=COMPLETE_EXPRESSION() <BRACE_CLOSE> continuation=SINGLE_QUOTE_CONTINUE()
    | token=<ESCAPED_QUOTE_PAIR> continuation=SINGLE_QUOTE_CONTINUE()
    | token=<ESCAPED_INTERPOLATION> continuation=SINGLE_QUOTE_CONTINUE()
    | token=<SINGLE_QUOTE_CHARS> continuation=SINGLE_QUOTE_CONTINUE()
    | <SINGLE_QUOTE_END>
    ) {
      if (continuation == null) {
        return new ArrayList<Map.Entry<String, Expr.Parsed>>();
      } else {
        String value = null;

        if (token != null) {
          value = token.image;

          if (value.equals("'''")) {
            value = "''";
          } else if (value.equals("''${")) {
            value = "${";
          }
        }

        continuation.add(new SimpleImmutableEntry<>(value, expr));
        return continuation;
      }
    }
}

Expr.Parsed SINGLE_QUOTE_LITERAL(): {
  List<Map.Entry<String, Expr.Parsed>> chunks;
  Token first;
} {
  (
    first=<SINGLE_QUOTE_START> chunks=SINGLE_QUOTE_CONTINUE()
  ) {
    return ParsingHelpers.makeSingleQuotedTextLiteral(chunks, first);
  }
}

Expr.Parsed IDENTIFIER(): {
  Token value;
  Token whsp0 = null;
  Token whsp1 = null;
  Token index = null;
} {
  (
      (
        (value=<QUOTED_LABEL> | value=<SIMPLE_LABEL>)
        (
          LOOKAHEAD(2)
          [whsp0=<WHSP>] <AT_SIGN>
          [whsp1=<WHSP>]
          index=<NATURAL_LITERAL>
        )?
      )
    | value=<BUILT_IN>
    | value=<TEXT>
    | value=<LOCATION>
  ) {
    return (index == null)
      ? ParsingHelpers.makeBuiltInOrIdentifier(value)
      : ParsingHelpers.makeIdentifier(value, whsp0, whsp1, index);
  }
}

Expr.Parsed NON_EMPTY_LIST_LITERAL(): {
  Token first;
  Token last;
  Token t0 = null;
  Token t1 = null;
  Token t2 = null;
  List<String> other = new ArrayList<>();
  StringBuilder currentOther = new StringBuilder("[");
  List<Expr.Parsed> values = new ArrayList<>();
  Expr.Parsed current;
} {
  (
    first=<BRACKET_OPEN>
    (t0=<WHSP> { currentOther.append(t0.image); })?
    (t0=<COMMA> { currentOther.append(t0.image); } (t0=<WHSP> { currentOther.append(t0.image); })?)?
    current=BASE_EXPRESSION() {
      values.add(current);
      other.add(currentOther.toString());
      currentOther.setLength(0);
    }
    (t0=<WHSP> { currentOther.append(t0.image); })?
    (
      t0=<COMMA> { currentOther.append(t0.image); }
      (t0=<WHSP> { currentOther.append(t0.image); })?
      current=BASE_EXPRESSION() {
        values.add(current);
        other.add(currentOther.toString());
        currentOther.setLength(0);
      }
      (t0=<WHSP> { currentOther.append(t0.image); })?
    )*
    last=<BRACKET_CLOSE>
  ) {
    currentOther.append(']');
    other.add(currentOther.toString());
    return ParsingHelpers.makeNonEmptyListLiteral(values, other, first, last);
  }
}


List<Map.Entry<List<String>, Expr.Parsed>> RECORD_LITERAL_ENTRY(String firstLabel): {
  List<Map.Entry<List<String>, Expr.Parsed>> values = new ArrayList<>();
  List<Map.Entry<List<String>, Expr.Parsed>> next = null;
  List<String> current = new ArrayList<>();
  current.add(firstLabel);
  Expr.Parsed expr;
} {
  (
    (
      (
        (
          <EQUAL_SIGN> |
          ((<DOT> [<WHSP>] firstLabel=ANY_LABEL_OR_SOME() { current.add(firstLabel); })+ [<WHSP>] <EQUAL_SIGN>)
        )
        [<WHSP>]
        expr=BASE_EXPRESSION() {
          values.add(new SimpleImmutableEntry<>(current, expr));
          current = new ArrayList<>();
        }
        [<WHSP>]
      ) |
      ({} { values.add(new SimpleImmutableEntry<List<String>, Expr.Parsed>(current, null)); current = new ArrayList<>(); })
    )
      (
        <COMMA>
        [<WHSP>]
        firstLabel=ANY_LABEL_OR_SOME() { current.add(firstLabel); }
        [<WHSP>]
        (
          (
            (
              <EQUAL_SIGN> |
              ((<DOT> [<WHSP>] firstLabel=ANY_LABEL_OR_SOME() { current.add(firstLabel); })+ [<WHSP>] <EQUAL_SIGN>)
            )
            [<WHSP>]
            expr=BASE_EXPRESSION() {
              values.add(new SimpleImmutableEntry<>(current, expr));
              current = new ArrayList<>();
            }
            [<WHSP>]
          ) |
          ({} { values.add(new SimpleImmutableEntry<List<String>, Expr.Parsed>(current, null)); current = new ArrayList<>(); })
        )
      )*
      <BRACE_CLOSE>
  ) {
    return values;
  }
}

List<Map.Entry<String, Expr.Parsed>> RECORD_TYPE_ENTRY(String firstLabel): {
  List<Map.Entry<String, Expr.Parsed>> values = new ArrayList<>();
  List<Map.Entry<String, Expr.Parsed>> next = null;
  Expr.Parsed expr;
} {
  (
    <COLON> <WHSP>
    expr=BASE_EXPRESSION() { values.add(new SimpleImmutableEntry<>(firstLabel, expr)); }
    [<WHSP>]
    (
      <COMMA>
      [<WHSP>]
      firstLabel=ANY_LABEL_OR_SOME()
      [<WHSP>] <COLON> <WHSP>
      expr=BASE_EXPRESSION() { values.add(new SimpleImmutableEntry<>(firstLabel, expr)); }
      [<WHSP>]
    )*
    <BRACE_CLOSE>
  ) {
    return values;
  }
}

Expr.Parsed RECORD_LITERAL_OR_TYPE(): {
  Token first;
  Token last = null;
  String firstLabel = null;
  List<Map.Entry<List<String>, Expr.Parsed>> literalValues = null;
  List<Map.Entry<String, Expr.Parsed>> typeValues = null;
} {
  (
    first=<BRACE_OPEN>
    [<WHSP>]
    (<COMMA> [<WHSP>])?
    (
        last=<BRACE_CLOSE>
      | (<EQUAL_SIGN> [<WHSP>] last=<BRACE_CLOSE> { literalValues = new ArrayList<>(); })
      | (
        firstLabel=ANY_LABEL_OR_SOME()
        [<WHSP>]
        (typeValues=RECORD_TYPE_ENTRY(firstLabel) | literalValues=RECORD_LITERAL_ENTRY(firstLabel))
      )
    )
  ) {
    if (literalValues != null) {
      return ParsingHelpers.makeRecordLiteral(literalValues, first, last);
    } else if (typeValues != null) {
      return ParsingHelpers.makeRecordType(typeValues, first, last);
    } else {
      return ParsingHelpers.makeRecordType(new ArrayList<Map.Entry<String, Expr.Parsed>>(), first, last);
    }
  }
}

Expr.Parsed UNION_TYPE(): {
  Token first;
  Token last;
  String label;
  Expr.Parsed type = null;
  List<Map.Entry<String, Expr.Parsed>> typeValues = new ArrayList<>();
} {
  (
    first=<ANGLE_OPEN>
    [<WHSP>]
    [<BAR> [<WHSP>]]
    (
        last=<ANGLE_CLOSE>
      | (
        (
          label=ANY_LABEL_OR_SOME()
          [<WHSP>]
          (
            [<COLON> <WHSP> type=BASE_EXPRESSION() [<WHSP>]] {
              typeValues.add(new SimpleImmutableEntry<>(label, type));
              type = null;
            }
            (
              <BAR>
              [<WHSP>]
              label=ANY_LABEL_OR_SOME()
              [<WHSP>]
              [<COLON> <WHSP> type=BASE_EXPRESSION() [<WHSP>]] {
                typeValues.add(new SimpleImmutableEntry<>(label, type));
                type = null;
              }
            )*
          )
        )
        last=<ANGLE_CLOSE>
      )
    )
  ) {
    return ParsingHelpers.makeUnionType(typeValues, first, last);
  }
}

Expr.Parsed PARENTHESIZED_EXPRESSION(): {
  Expr.Parsed value;
  Token first;
  Token last;
} {
  (first=<PARENS_OPEN> value=COMPLETE_EXPRESSION() last=<PARENS_CLOSE>) {
    return ParsingHelpers.makeParenthesized(value, first, last);
  }
}

Expr.Parsed DOUBLE_LITERAL():  { Token token; } { token=<DOUBLE_LITERAL>  { return ParsingHelpers.makeDoubleLiteral(token);  }}
Expr.Parsed INTEGER_LITERAL(): { Token token; } { token=<INTEGER_LITERAL> { return ParsingHelpers.makeIntegerLiteral(token); }}
Expr.Parsed NATURAL_LITERAL(): { Token token; } { token=<NATURAL_LITERAL> { return ParsingHelpers.makeNaturalLiteral(token); }}

Expr.Parsed PRIMITIVE_EXPRESSION(): { Expr.Parsed expr; } {
  (
      expr=DOUBLE_LITERAL()
    | expr=NATURAL_LITERAL()
    | expr=INTEGER_LITERAL()
    | expr=DOUBLE_QUOTE_LITERAL()
    | expr=SINGLE_QUOTE_LITERAL()
    | expr=RECORD_LITERAL_OR_TYPE()
    | expr=UNION_TYPE()
    | expr=NON_EMPTY_LIST_LITERAL()
    | expr=IDENTIFIER()
    | expr=PARENTHESIZED_EXPRESSION()
  ) { return expr; }
}

Expr.Parsed FIELD_ACCESS_EXPRESSION(Expr.Parsed base, Token whsp0, Token whsp1): {
  Token token;
} {
  (token=<SIMPLE_LABEL> | token=<QUOTED_LABEL> | token=<TEXT> | token=<LOCATION> | token=<BUILT_IN>) {
    return ParsingHelpers.makeFieldAccess(base, trimLabel(token.image), whsp0, whsp1, token.endLine, token.endColumn);
  }
}

Expr.Parsed PROJECTION_EXPRESSION(Expr.Parsed base, Token whsp0, Token whsp): {
  String current;
  List<String> labels = new ArrayList<String>();
  Token last;
} {
  (
    <BRACE_OPEN> [<WHSP>]
    (
      current=ANY_LABEL_OR_SOME() { labels.add(current); }
      [<WHSP>]
      (<COMMA> [<WHSP>] current=ANY_LABEL_OR_SOME() { labels.add(current); } [<WHSP>])*
    )?
    last=<BRACE_CLOSE>
  ) {
    return ParsingHelpers.makeProjection(base, labels, last.endLine, last.endColumn);
  }
}

Expr.Parsed PROJECTION_BY_TYPE_EXPRESSION(Expr.Parsed base, Token whsp0, Token whsp): {
  Expr.Parsed expr;
  Token last;
} {
  (<PARENS_OPEN> [<WHSP>] expr=BASE_EXPRESSION() [<WHSP>] last=<PARENS_CLOSE>) {
    return ParsingHelpers.makeProjectionByType(base, expr, last.endLine, last.endColumn);
  }
}

Expr.Parsed SELECTOR_EXPRESSION(): {
  Expr.Parsed base;
  Token whsp0 = null;
  Token whsp1 = null;
} {
  (
    base=PRIMITIVE_EXPRESSION()
    (
      LOOKAHEAD(2)
      [whsp0=<WHSP>] <DOT> [whsp1=<WHSP>]
      (
        base=FIELD_ACCESS_EXPRESSION(base, whsp0, whsp1)
      | base=PROJECTION_EXPRESSION(base, whsp0, whsp1)
      | base=PROJECTION_BY_TYPE_EXPRESSION(base, whsp0, whsp1)
      ) {
        whsp0 = null;
        whsp1 = null;
      }
    )*
  ) {
    return base;
  }
}

Expr.Parsed COMPLETION_EXPRESSION(): {
  Expr.Parsed base;
  Expr.Parsed completion = null;
  Token operatorToken;
  Token whsp0 = null;
  Token whsp1 = null;
} {
  (
    base=SELECTOR_EXPRESSION()
    (
      LOOKAHEAD(2)
      [whsp0=<WHSP>] operatorToken=<COMPLETE> [whsp1=<WHSP>]
      completion=SELECTOR_EXPRESSION() {
        base = ParsingHelpers.makeOperatorApplication(Operator.COMPLETE, base, completion, operatorToken.image, whsp0, whsp1);
      }
    )?
  ) {
    return base;
  }
}

Expr.Parsed IMPORT(): {
  Token token = null;
  Token hash = null;
  Token asValue = null;
  Expr.Parsed using = null;
} {
  (
    (
        token=<MISSING>
      | token=<LOCAL>
      | (token=<HTTP> (LOOKAHEAD(2) [<WHSP>] <USING> <WHSP> using=IMPORT_TYPE_EXPRESSION())?)
      | token=<ENV>
      | token=<CLASSPATH>
    )
    (LOOKAHEAD(2) <WHSP> hash=<SHA256_HASH>)?
    (LOOKAHEAD(2) [<WHSP>] <AS> <WHSP> (asValue=<TEXT> | asValue=<LOCATION>))?
  ) {
    return ParsingHelpers.makeImport(token, hash, asValue, using);
  }
}

Expr.Parsed IMPORT_WITHOUT_CHECK(): {
  Token token = null;
  Token asValue = null;
  Expr.Parsed using = null;
} {
  (
    (
        token=<MISSING>
      | token=<LOCAL>
      | (token=<HTTP> (LOOKAHEAD(2) [<WHSP>] <USING> <WHSP> using=IMPORT_TYPE_EXPRESSION())?)
      | token=<ENV>
      | token=<CLASSPATH>
    )
    (LOOKAHEAD(2) [<WHSP>] <AS> <WHSP> (asValue=<TEXT> | asValue=<LOCATION>))?
  ) {
    return ParsingHelpers.makeImport(token, null, asValue, using);
  }
}

Expr.Parsed IMPORT_EXPRESSION(): { Expr.Parsed expr; } {
  (expr=IMPORT() | expr=COMPLETION_EXPRESSION()) { return expr; }
}

Expr.Parsed IMPORT_TYPE_EXPRESSION(): { Expr.Parsed expr; } {
  (expr=IMPORT_WITHOUT_CHECK() | expr=COMPLETION_EXPRESSION()) { return expr; }
}

Expr.Parsed APPLICATION_EXPRESSION(): {
  Token first = null;
  Token whsp0;
  Token whsp1;
  Expr.Parsed base = null;
  Expr.Parsed current0;
  Expr.Parsed current1;
  Token whsp2 = null;
  Token whsp3 = null;
  Expr.Parsed expr;
  Expr.Parsed other;
  Expr.Parsed type = null;
} {
  (
    (
      (first=<MERGE>
      whsp0=<WHSP>
      expr=IMPORT_EXPRESSION()
      whsp1=<WHSP>
      other=IMPORT_EXPRESSION()
      (LOOKAHEAD(2)
        [whsp2=<WHSP>]
        <COLON>
        whsp3=<WHSP>
        type=BASE_EXPRESSION()
      )?) {
        base = ParsingHelpers.makeMerge(expr, other, type, first, whsp0, whsp1, whsp2, whsp3);
      }
      |
      (first=<SOME> whsp0=<WHSP> current0=IMPORT_EXPRESSION() { base = ParsingHelpers.makeApplication(ParsingHelpers.makeBuiltInOrIdentifier(first), current0, whsp0); })
      |
      (first=<TOMAP>
      whsp0=<WHSP>
      expr=IMPORT_EXPRESSION()
      (LOOKAHEAD(2)
        [whsp2=<WHSP>]
        <COLON>
        whsp3=<WHSP>
        type=BASE_EXPRESSION()
      )?) {
          base = ParsingHelpers.makeToMap(expr, type, first, whsp0, whsp2, whsp3);
      }
      | base=IMPORT_EXPRESSION()
    ) (
      LOOKAHEAD(2)
      whsp0=<WHSP> current0=IMPORT_EXPRESSION() { base = ParsingHelpers.makeApplication(base, current0, whsp0); }
    )*
  ) {
    return base;
  }
}

Map.Entry<String, Operator> OPERATOR(): {
  Token token;
  Token whsp = null;
  Operator operator;
} {
  (
      token=<OR> { operator = Operator.OR; }
    | token=<AND> { operator = Operator.AND; }
    | token=<EQUALS> { operator = Operator.EQUALS; }
    | token=<NOT_EQUALS> { operator = Operator.NOT_EQUALS; }
    | (token=<PLUS> whsp=<WHSP>) { operator = Operator.PLUS; }
    | token=<TIMES> { operator = Operator.TIMES; }
    | token=<TEXT_APPEND> { operator = Operator.TEXT_APPEND; }
    | token=<LIST_APPEND> { operator = Operator.LIST_APPEND; }
    | token=<COMBINE> { operator = Operator.COMBINE; }
    | token=<PREFER> { operator = Operator.PREFER; }
    | token=<COMBINE_TYPES> { operator = Operator.COMBINE_TYPES; }
    | (token=<IMPORT_ALT> whsp=<WHSP>) { operator = Operator.IMPORT_ALT; }
    | token=<EQUIVALENT> { operator = Operator.EQUIVALENT; }
  ) {
    if (whsp == null) {
      return new SimpleImmutableEntry<>(token.image, operator);
    } else {
      return new SimpleImmutableEntry<>(token.image + whsp.image, operator);
    }
  }
}

Expr.Parsed OPERATOR_EXPRESSION(int minPredecence): {
  Expr.Parsed base;
  Expr.Parsed arg;
  Map.Entry<String, Operator> operator;
  Map.Entry<List<String>, Expr.Parsed> current;
  Token whsp0 = null;
  Token whsp1 = null;
} {
  (
    base=APPLICATION_EXPRESSION()
    (LOOKAHEAD({
      getToken(1).kind == WHSP && getToken(2).kind == WITH && minPredecence < 1
    })
      whsp0=<WHSP> <WITH> <WHSP>
      current=WITH_ENTRY() { base = ParsingHelpers.makeWith(base, current.getKey(), current.getValue(), whsp0); }
    )*
    (LOOKAHEAD({
      (getToken(1).kind == WHSP && OperatorPrecedenceTable.get(getToken(2).kind) >= minPredecence) ||
      OperatorPrecedenceTable.get(getToken(1).kind) >= minPredecence
    })
    [whsp0=<WHSP>]
    operator=OPERATOR() [whsp1=<WHSP>] arg=OPERATOR_EXPRESSION(operator.getValue().getPrecedence() + 1) {
      base = ParsingHelpers.makeOperatorApplication(operator.getValue(), base, arg, operator.getKey(), whsp0, whsp1);
      whsp0 = null;
    })*
  ) {
    return base;
  }
}

Map.Entry<List<String>, Expr.Parsed> WITH_ENTRY(): {
  List<String> labels = new ArrayList<String>();
  String current;
  Expr.Parsed expr;
} {
  (
    current=ANY_LABEL_OR_SOME() { labels.add(current); }
    (LOOKAHEAD(2) [<WHSP>] <DOT> [<WHSP>] current=ANY_LABEL_OR_SOME() { labels.add(current); })*
    [<WHSP>]
    <EQUAL_SIGN>
    [<WHSP>]
    expr=OPERATOR_EXPRESSION(1)
  ) {
    return new SimpleImmutableEntry<>(labels, expr);
  }
}

Expr.Parsed EMPTY_LIST_LITERAL(): {
  Expr.Parsed type;
  Token first;
  Token t0;
  StringBuilder builder = new StringBuilder("[");
} {
  (
    first=<BRACKET_OPEN>
    (t0=<WHSP> { builder.append(t0.image); })?
    (<COMMA> { builder.append(','); } (t0=<WHSP> { builder.append(t0.image); })?)?
    <BRACKET_CLOSE> { builder.append(']'); }
    (t0=<WHSP> { builder.append(t0.image); })?
    <COLON>
    t0=<WHSP> {
      builder.append(':');
      builder.append(t0.image);
    }
    type=BASE_EXPRESSION()
  ) {
    return ParsingHelpers.makeEmptyListLiteral(type, builder.toString(), first);
  }
}

Expr.Parsed LAMBDA_EXPRESSION(): {
  Expr.Parsed type;
  Expr.Parsed body;
  Token first;
  String name;
} {
  (
    first=<LAMBDA> [<WHSP>] <PARENS_OPEN> [<WHSP>]
    name=NAME_BINDING() [<WHSP>] <COLON> <WHSP>
    type=BASE_EXPRESSION() [<WHSP>] <PARENS_CLOSE> [<WHSP>] <ARROW> [<WHSP>]
    body=BASE_EXPRESSION()
  ) {
    return ParsingHelpers.makeLambda(name, type, body, first);
  }
}

LetBinding LET_BINDING(): {
  Token first;
  Token whsp;
  String name;
  Expr.Parsed type = null;
  Expr.Parsed value;
  StringBuilder builder = new StringBuilder();
  String text0 = null;
  String text2 = null;
} {
  (
    first=<LET>
    whsp=<WHSP>
    name=NAME_BINDING() {
      builder.append(first.image);
      builder.append(whsp.image);
      builder.append(name);
    }
    (whsp=<WHSP> { builder.append(whsp.image); })?
    (
      <COLON> whsp=<WHSP> {
        builder.append(':');
        builder.append(whsp.image);
        text0 = builder.toString();
        builder.setLength(0);
      }
      type=BASE_EXPRESSION()
      (whsp=<WHSP> { builder.append(whsp.image); })?
    )?
    <EQUAL_SIGN> { builder.append('='); }
    (whsp=<WHSP> { builder.append(whsp.image); })?
    value=BASE_EXPRESSION()
    (whsp=<WHSP> { text2 = whsp.image; })?
  ) {
    return new LetBinding(name, type, value, text0, builder.toString(), text2, first.beginLine, first.beginColumn);
  }
}

Expr.Parsed LET_EXPRESSION(): {
  LetBinding current = null;
  List<LetBinding> bindings = new ArrayList<>();
  Expr.Parsed body;
  Token whsp;
} {
  (
    (current=LET_BINDING() { bindings.add(current); })+ <IN> whsp=<WHSP> body=BASE_EXPRESSION()
  ) {
    return ParsingHelpers.makeLet(bindings, body, whsp.image);
  }
}

Expr.Parsed IF_EXPRESSION(): {
  Token first;
  Expr.Parsed predicate;
  Expr.Parsed thenValue;
  Expr.Parsed elseValue;
} {
  (
    first=<IF> <WHSP> predicate=BASE_EXPRESSION() [<WHSP>] <THEN> <WHSP> thenValue=BASE_EXPRESSION() [<WHSP>] <ELSE> <WHSP> elseValue=BASE_EXPRESSION()
  ) {
    return ParsingHelpers.makeIf(predicate, thenValue, elseValue, first);
  }
}

Expr.Parsed FORALL_EXPRESSION(): {
  Token first;
  String name;
  Expr.Parsed input;
  Expr.Parsed result;
} {
  (
    first=<FORALL> [<WHSP>] <PARENS_OPEN> [<WHSP>]
    name=NAME_BINDING() [<WHSP>] <COLON> <WHSP>
    input=BASE_EXPRESSION() [<WHSP>] <PARENS_CLOSE> [<WHSP>] <ARROW> [<WHSP>]
    result=BASE_EXPRESSION()
  ) {
    return ParsingHelpers.makePi(name, input, result, first);
  }
}

Expr.Parsed ASSERT_EXPRESSION(): {
  Token first;
  Token whsp0 = null;
  Token whsp1 = null;
  Expr.Parsed value;
} {
  (first=<ASSERT> [whsp0=<WHSP>] <COLON> whsp1=<WHSP> value=BASE_EXPRESSION()) {
    return ParsingHelpers.makeAssert(value, first, whsp0, whsp1);
  }
}

Expr.Parsed FUNCTION_TYPE_OR_ANNOTATED_EXPRESSION(): {
  Expr.Parsed base;
  Expr.Parsed type = null;
  Expr.Parsed result = null;
  Token whsp0 = null;
  Token whsp1 = null;
} {
  (
    base=OPERATOR_EXPRESSION(0)
    (LOOKAHEAD(2)
      [whsp0=<WHSP>]
      ((<ARROW> [<WHSP>] result=BASE_EXPRESSION()) |
      (<COLON> whsp1=<WHSP> type=BASE_EXPRESSION()))
    )?
  ) {
    if (type == null) {
      if (result == null) {
        return base;
      } else {
        return ParsingHelpers.makePi(base, result);
      }
    } else {
      return ParsingHelpers.makeAnnotated(base, type, whsp0, whsp1);
    }
  }
}

Expr.Parsed BASE_EXPRESSION(): { Expr.Parsed expr; } {
  (
      expr=LAMBDA_EXPRESSION()
    | expr=IF_EXPRESSION()
    | expr=LET_EXPRESSION()
    | expr=FORALL_EXPRESSION()
    | LOOKAHEAD(EMPTY_LIST_LITERAL()) expr=EMPTY_LIST_LITERAL()
    | expr=ASSERT_EXPRESSION()
    | expr=FUNCTION_TYPE_OR_ANNOTATED_EXPRESSION()
  ) {
    return expr;
  }
}

Expr.Parsed COMPLETE_EXPRESSION(): { Expr.Parsed expr; } { ([<WHSP>] expr=BASE_EXPRESSION() [<WHSP>]) { return expr; }}

Expr.Parsed TOP_LEVEL(): { Expr.Parsed expr; } { (expr=COMPLETE_EXPRESSION() <EOF>) { return expr; }}
